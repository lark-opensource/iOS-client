# Copyright 2020 The Lynx Authors. All rights reserved.

import("//Lynx/Lynx.gni")
import("//testing/test.gni")

animation_shared_sources = [
  "animation_delegate.h",
  "animation.cc",
  "animation.h",
  "animation_curve.cc",
  "animation_curve.h",
  "animation_vsync_proxy.cc",
  "animation_vsync_proxy.h",
  "keyframe_effect.cc",
  "keyframe_effect.h",
  "keyframe_model.cc",
  "keyframe_model.h",
  "keyframed_animation_curve.cc",
  "keyframed_animation_curve.h",
  "timing_function.cc",
  "timing_function.h",
  "util/cubic_bezier.cc",
  "util/cubic_bezier.h",
  "css_keyframe_manager.cc",
  "css_keyframe_manager.h",
  "css_transition_manager.cc",
  "css_transition_manager.h"
]

lynx_source_set("animation") {
  sources = animation_shared_sources

  public_deps = [
    "//third_party/rapidjson",
  ]
  if (!is_android) {
    public_deps += [
      "//Lynx/base",
      "//Lynx/lepus",
    ]
  }
}

lynx_source_set("animation_oliver") {
  sources = animation_shared_sources
}

unittest_set("animation_testset") {
  testonly = true

  public_deps = [
    "//Lynx/animation",
  ]

  deps = [
    "//Lynx/shell:mock_tasm_delegate",
    "//Lynx/tasm"
  ]

  sources = [
    "animation_unittest.cc",
    "animation_vsync_proxy_unittest.cc",
    "css_keyframe_manager_unittest.cc",
    "css_transition_manager_unittest.cc", 
    "keyframe_effect_unittest.cc",
    "keyframe_model_unittest.cc",
    "keyframed_animation_curve_unittest.cc",
    "testing/mock_animation.cc",
    "testing/mock_css_keyframe_manager.cc",
    "testing/mock_css_transition_manager.cc"
  ]
}

unit_test("animation_unittests") {
  testonly = true
  sources = []
  deps = [
    "//Lynx/animation",
    "//Lynx/animation:animation_testset",
    "//Lynx/tasm/react:dom"
  ]
}

group("animation_tests") {
  testonly = true
  deps = [
    "//Lynx/animation:animation_unittests"
  ]
  public_deps = [
    "//Lynx/animation:animation_testset"
  ]
}

gen_subspec_file("animation_podspec") {
  parent_path = "subspecs-Native"
  podspec_name = "animation"
  sources = animation_shared_sources
}
